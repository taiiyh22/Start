//===== rAthena Script ======================================= 
//= Player Marriage
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Official Marriage script.
//= Added isloggedin() check, even though Aegis doesn't do it.
//= Currently does not support same-Sex marriages.
//= Old script located in: npc/custom/marriage.txt
//= - Variable in use: wedding_sign (max 1)
//= - Variable in use: $@wedding (max 1) 
//= - Variable in use: $@wed_groom$ $@wed_bride$
//===== Additional Comments: ================================= 
//= 1.0 First version. [L0ne_W0lf]
//= 1.1 Fixed a missing ")" [L0ne_W0lf]
//= 1.2 Corrected duration of Wedding status. [L0ne_W0lf]
//= 1.3 Changed permanent global variables to temporary ones. [Euphy]
//=     Added GM NPCs (commented).
//= 1.4 Added GM management function. [Euphy]
//= 1.5 Added Marriage Covenant item and VIP features. [Euphy]
//============================================================

prt_church,97,100,4	script	Wedding Staff#w	71,{
	cutin "wedding_marry01",2;
	if (countitem(6026) == 0) {

		// If the VIP system is enabled:
		// - Non-VIP players must use a Marriage Covenant to get married.
		// - VIP players may either pay the Zeny fee or use a Marriage Covenant.
		if (VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE)) {
			mes "[Marry Happy]";
			mes "ขออภัยด้วยแต่เจ้าไม่สามารถแต่งงานได้ในตอนนี้.";
			mes "งานแต่งงานนี้มีให้เฉพาะสำหรับผู้เล่นที่เป็นสมาชิกVIPเท่านั้น.";
			mes "แต่ถ้าเจ้ามี ^FF0000Marriage Covenant^000000, เจ้าจึงจะสามารถแต่งงานได้!";
			goto L_End;
		}

	} else
		set .@covenant,1;
	if (Upper == 2) {
		mes "[Marry Happy]";
		mes "หวัดดี~";
		mes "ข้ามีชื่อว่า Marry Happy";
		mes "และข้าอยู่ที่นี่เพื่อนำเสนอเจ้า";
		mes "เกี่ยวกับรายละเอียดของ";
		mes "การแต่งงาน. แล้วเจ้า";
		mes "มีคำถามอะไรไหม ?";
		next;
		if (select("ข้าอยากแต่งงาน.:ข้าไม่ต้องการความช่วยเหลือ!") == 1) {
			mes "[Marry Happy]";
			mes "โอ้, ข้าขอโทษ, แต่";
			mes "ตัวละครที่เป็นลูกบุญธรรม";
			mes "นั้นไม่สามารถ";
			mes "แต่งงานได้. ดังนั้นแล้ว, ทำไมเจ้า";
			mes "ไม่เล่นแบบปกติล่ะ";
			mes "มีความสุขแบบวัยเด็ก ?";
			goto L_End;
		}
		mes "[Marry Happy]";
		mes "โอ้, แน่นอน";
		mes "แต่! เด็กน้อย";
		mes "เจ้าแต่งงานไม่ได้-- เพราะว่า";
		mes "มันผิดกฎหมายน่ะสิ";
		mes "นั่น~ ไม่ได้ทำให้เจ้า";
		mes "น่ารักหรอกหรือ ?";
		goto L_End;
	}
	mes "[Marry Happy]";
	mes "การแต่งงานคือความสวยงาม";
	mes "รวมวิญญาณสองดวงเข้าด้วยกัน";
	mes "เลือกที่จะอยู่ด้วยกันไปจนแก่เฒ่า,";
	mes "แลกเปลี่ยนความสุขและความทุกข์.";
	mes "มันเป็นสิ่งที่พิเศษของใครบางคน";
	mes "เช่นเดียวกับตัวเจ้าตอนนี้ ?";
	next;
	switch(select("ถามเกี่ยวกับพิธีแต่งงาน:ถามเกี่ยวกับขั้นตอน:ตกลงที่จะแต่งงาน:พวกเราคือกองทัพคนโสด!")) {
	case 1:
		mes "[Marry Happy]";
		mes "ราชา Tristram III";
		mes "ผู้ปราดเปรืองและใจดี";
		mes "เคยเป็นผู้ดำเนินงานพิธีแต่งงาน,";
		mes "แต่ก็เป็นได้ไม่นาน";
		mes "เพราะติดงานในราชวัง";
		mes "และไม่ค่อยมีเวลาว่างมากนัก.";
		next;
		mes "[Marry Happy]";
		mes "บาทหลวง Vomars,";
		mes "บาทหลวงแห่งความรัก";
		mes "ซึ่งตอนนี้เป็นผู้ดำเนินงานพิธีแต่งงาน.";
		mes "เขาเป็นดั่งสมบัติแห่ง";
		mes "อาณาจักร Rune-Midgarts.";
		next;
		mes "[Marry Happy]";
		mes "เมื่อเจ้าต้องแต่งงานกับใครสักคน,";
		mes "มันจะเป็นช่วงเวลาที่ดีหรือแย่ของชีวิตของเจ้า, ดังนั้นจงคิดให้ดี";
		mes "ก่อนที่จะตกลงแต่งงาน.";
		mes "โปรดจำเอาไว้ว่าผู้ชายเท่านั้นที่สามารถ";
		mes "แต่งงานกับผู้หญิงได้.";
		next;
		mes "[Marry Happy]";
		mes "ถ้าเจ้าโชคดีพอ";
		mes "ที่จะหาคู่ชีวิตได้";
		mes "และต้องการที่จะใช้ชีวิตร่วมกัน";
		mes "ข้าได้แต่หวังว่า.";
		mes "จะเป็นคู่ที่เหมาะสมกัน~";
		break;
	case 2:
		mes "[Marry Happy]";
		mes "ในขั้นแรกของ";
		mes "พิธีแต่งงาน";
		mes "ให้เริ่มจาก.";
		mes "เจ้าบ่าวและเจ้าสาว";
		mes "ทั้งสองคน";
		mes "ต้องอยู่ในปาร์ตี้เดียวกัน.";
		next;
		mes "[Marry Happy]";
		mes "หลังจากปาร์ตี้กันแล้ว,";
		mes "ทั้งคู่ต้องคุยกับ";
		mes "บาทหลวง Vomars. เจ้าบ่าว";
		mes "ต้องคุยก่อนและบอกชื่อ";
		mes "เจ้าสาวกับบาทหลวงไป. มิฉะนั้น, พิธีแต่งงานจะถูกหยุดไว้.";
		next;
		mes "[Marry Happy]";
		mes "ต่อมา, เจ้าสาว";
		mes "คุยกับบาทหลวงและบอกชื่อ";
		mes "เจ้าบ่าวแก่บาทหลวงไป.";
		mes "ถ้าชื่อที่บอกกับบาทหลวง";
		mes "ถูกต้อง, พวกเจ้าถึงจะ";
		mes "สามารถแลกแหวนกันได้.";
		next;
		mes "[Marry Happy]";
		mes "แหวนแต่งงานที่";
		mes "แลกเปลี่ยนกัน, จะทำให้ทั้งคู่ถูกผูกมัดไว้ตลอดไป. แน่นอนว่า,";
		mes "ก่อนหน้านั้น, มีโอกาสที่จะเปลี่ยนความคิดของเจ้า, ดังนั้น...";
		next;
		mes "[Marry Happy]";
		mes "ถ้ามีคู่รักจำนวนมาก";
		mes "ที่ต้องการจะแต่งงาน";
		mes "ในเวลาเดียวกัน, กรุณา";
		mes "ต่อแถวและคุยกับบาทหลวง Vomars";
		mes "เพราะจะมีเพียงคู่เดียวเท่านั้นที่สามารถแต่งงานกันได้.";
		next;
		mes "[Marry Happy]";
		mes "สุดท้าย, อย่าลืมบอก";
		mes "บาทหลวง Vomars ด้วย";
		mes "ชื่อของคู่ของเจ้านานเกินไป. มิฉะนั้น,";
		mes "พิธีแต่งงานจะถูกยกเลิกและเจ้าต้องทำใหม่.";
		next;
		mes "[Marry Happy]";
		mes "เจ้าสาวต้องจำไว้";
		mes "ว่ามีเวลาแค่ 3 นาที";
		mes "ที่จะคุยกับบาทหลวง Vomars";
		mes "หลังจากเจ้าบ่าว";
		mes "คุยกับบาทหลวงเสร็จแล้ว.";
		next;
		if (select("ขอบคุณ, ช่วยได้มากเลย!:มีวิธีที่ง่ายในการบอกชื่อคู่ของข้าไหม ?") == 1) {
			mes "[Marry Happy]";
			mes "ข้าอยู่ที่นี้เพื่อช่วยให้";
			mes "พิธีแต่งงาน";
			mes "ราบรื่นอยู่แล้ว.";
			mes "ถ้ามีบางสิ่งที่";
			mes "เจ้าไม่เข้าใจ,";
			mes "ให้ถามข้าอีกได้เลย.";
			goto L_End;
		}
		mes "[Marry Happy]";
		mes "วิธีง่ายๆในการพิมพ์";
		mes "ชื่อคู่รักของเจ้าคือ";
		mes "ให้พิมพ์ข้อความส่วนตัว";
		mes "ไปหาคู่ของเจ้า, และ";
		mes "กด คลิก-ซ้าย ที่ช่องชื่อ.";
		next;
		mes "[Marry Happy]";
		mes "กดปุ่ม ''Ctrl'' และ ''C''";
		mes "เพื่อ copy ชื่อ. จากนั้น,";
		mes "เจ้าสามารถกด";
		mes "วางชื่อโดยกด";
		mes "ปุ่ม ''Insert'' และ ''Shift''. เป็นไงง่ายใช่ไหม ?";
		next;
		mes "[Marry Happy]";
		mes "เอาล่ะ, ตอนนี้";
		mes "เพื่อเป็นการฝึกวาง";
		mes "ชื่อคู่รักของเจ้า";
		mes "ตามวิธีที่ข้าได้บอกเจ้าไป.";
		next;
		input .@partner$;
		mes "[Marry Happy]";
		mes "ยอดเยี่ยม, หลังจากที่เจ้า";
		mes "ตัดสินใจที่จะแต่งงาน,";
		mes "ให้กลับมาหาข้าและ";
		mes "ส่งใบสมัครของเจ้ามา.";
		mes "ข้าจะรอเจ้าอยู่ที่นี้, นักผจญภัย~";
		break;
	case 3:
		cutin "wedding_marry02",2;
		if (VIP_SCRIPT) {
			mes "[Marry Happy]";
			mes "^FF0000รอก่อน!^000000";
			mes "^FF0000ถ้า account ของเจ้าไม่ใช่ premium, เจ้าต้องมี Marriage Covenant เพื่อแต่งงาน.^000000";
			mes "^FF0000กรุณาตรวจสอบ account ของเจ้า, และคู่รักของเจ้า, ก่อนที่จะลงทะเบียนแต่งงาน.^000000";
			next;
		}
		if (Sex == SEX_MALE) {
			mes "[Marry Happy]";
			mes "เจ้าอยากจะแต่งงานใช่ไหม ?";
			mes "เจ้าบ่าว, เจ้าต้องเตรียม";
			if (.@covenant) {
				mes "^3377FF1 Tuxedo^000000 และ ^3377FF1 Marriage Covenant^000000.";
				mes "เจ้าสาวจะต้องสวมชุดแต่งงานมาด้วยและเงิน";
				mes "1,200,000 zeny หรือใช้ Marriage Covenant.";
			} else {
				mes "^3377FF1 Tuxedo^000000 และเงิน ^3377FF1,300,000 zeny^000000.";
				mes "เจ้าสาวจะต้องสวมชุดแต่งงานมาด้วยและเงิน";
				mes "1,200,000 zeny.";
			}
		} else {
			mes "[Marry Happy]";
			mes "เจ้าอยากจะแต่งงานใช่ไหม ?";
			mes "เจ้าสาว, เจ้าต้องเตรียม";
			if (.@covenant) {
				mes "^3377FF1 Wedding Dress^000000 และ ^3377FFMarriage Covenant^000000.";
				mes "เจ้าบ่าวต้องมี Tuxedo และเงิน";
				mes "1,300,000 zeny หรือใช้ a Marriage Covenant.";
			} else {
				mes "^3377FF1 Wedding Dress^000000 และเงิน 1,200,000 zeny. เจ้าบ่าว";
				mes "ต้องมี Tuxedo และเงิน 1,300,000 zeny เพื่อแต่งงาน.";
			}
		}
		next;
		mes "[Marry Happy]";
		mes "เจ้าบ่าวและเจ้าสาว";
		mes "ต้องมี ^3377FF1 Diamond Ring^000000 เพื่อ";
		mes "แลกแหวนกัน.";
		mes "เจ้าจะต้องเตรียมของให้พร้อม";
		mes "เมื่อเจ้าลงทะเบียน";
		mes "แต่งงานแล้ว.";
		next;
		mes "[Marry Happy]";
		mes "คาดหวังว่าเจ้าสาว";
		mes "และเจ้าบ่าวจะทำพิธี";
		mes "ลงทะเบียนจนเสร็จก่อนที่";
		mes "จะเริ่มพิธีงานแต่งงาน.";
		mes "ตอนนี้, เจ้าต้องการที่จะ";
		mes "แต่งงานไหม ?";
		next;
		if (select("ใช่:ไม่") == 1) {
			if (getpartnerid()) {
				cutin "wedding_marry02",2;
				mes "[Marry Happy]";
				mes "ข้าขอโทษด้วย, แต่เจ้าไม่สามารถ";
				mes "แต่งงานใหม่ได้!";
				mes "ข้าไม่สามารถให้เจ้าหักหลัง";
				mes "คู่สมรสของเจ้าได้,";
				mes "นอกเหนือจากว่า, คู่ของเจ้า";
				mes "ไม่ถูกต้องตามกฎหมายของ Rune-Midgarts.";
				break;
			}
			else if (wedding_sign == 1) {
				mes "[Marry Happy]";
				mes "เจ้ายังไม่";
				mes "เสร็จสิ้นการลงทะเบียน ?";
				mes "หืมม, ดูให้แน่ใจว่า";
				mes "คู่ของเจ้า";
				mes "ได้ลงทะเบียนหรือยัง,";
				mes "โดยคุยกับบาทหลวง Vomars.";
				break;
			}
			else if (BaseLevel < 45) {
				mes "[Marry Happy]";
				mes "หืมม, เจ้าต้อง";
				mes "เก่งกว่านี้เพื่อปกป้อง";
				mes "คนรักของเจ้าได้ก่อน";
				mes "ที่เจ้าจะแต่งงาน.";
				mes "เมื่อเจ้าแข็งแกร่ง,";
				mes "ให้กลับมาคุยกับข้าอีกครั้ง.";
				goto L_End;
			}
			else if (countitem(2613) < 1) {
				mes "[Marry Happy]";
				mes "อืม? ดูเหมือนว่าเจ้า";
				mes "จะลืม Diamond Ring";
				mes "ที่ใช้แลกกับคู่ของเจ้า";
				mes "ในพิธีแต่งงาน ?";
				mes "กลับไปหามันมาซะ";
				mes "และนำมันมาด้วย, okay?";
				goto L_End;
			}
			else if (Sex == SEX_FEMALE) {
				if (countitem(6026) == 0 && Zeny < 1200000) {
					mes "[Marry Happy]";
					mes "ข้าขอโทษด้วย, แต่เจ้าสาว";
					mes "ต้องจ่ายเงิน 1,200,000";
					mes "zeny หรือนำ a Marriage";
					mes "Covenant มาด้วย";
					mes "ในตอนที่แต่งงาน. บางที";
					mes "เจ้าควรถามคู่ของเจ้า";
					mes "ให้ช่วยเรื่องนี้น่ะ ?";
					goto L_End;
				}
				else if (countitem(2338) < 1) {
					mes "[Marry Happy]";
					mes "โอ้ ที่รัก, เจ้าลืม";
					mes "สวมชุดแต่งงาน ?";
					mes "ใจเย็นๆและหามันมา";
					mes "ให้ข้า-- เจ้า";
					mes "ควรที่จะมีมัน";
					mes "เพื่องานแต่งงาน!";
					goto L_End;
				}
			}
			else if (Sex == SEX_MALE) {
				if (countitem(6026) == 0 && Zeny < 1300000) {
					mes "[Marry Happy]";
					mes "ข้าขอโทษด้วย, แต่เจ้า";
					mes "ไม่มีเงิน 1,300,000 zeny";
					mes "หรือ Marriage Covenant";
					mes "เจ้าบ่าวต้องจ่ายเงิน";
					mes "เป็นค่าพิธีแต่งงานน่ะ.";
					mes "เจ้าลืมเงินไว้หรือเปล่า ?";
					goto L_End;
				}
				else if (countitem(7170) < 1) {
					mes "[Marry Happy]";
					mes "ไหนล่ะ Tuxedo ของเจ้า ?";
					mes "เจ้าต้องมีมัน";
					mes "ไว้ใช้ในพิธีแต่งงาน";
					mes "หามันมา ! นำมันมา";
					mes "ให้ข้า, จะได้ทำมันให้เสร็จๆไป";
					mes "งานแต่งงาน, okay?";
					goto L_End;
				}
			}
			mes "[Marry Happy]";
			mes "เยี่ยม, ดูเหมือนว่าเจ้า";
			mes "พร้อมแล้วสิน่ะ.";
			mes "แม้ว่าข้าจะไม่รู้ว่าใคร";
			mes "คือคู่ของเจ้า, ให้ข้า";
			mes "เป็นคนแรกที่แสดงความยินดี";
			mes "ในงานแต่งงานของเจ้า~";
			next;
			mes "[Marry Happy]";
			mes "ตอนนี้, ได้เวลาเริ่ม";
			mes "พิธีแล้ว. กรุณาเขียน";
			mes "ชื่อคู่สมรสของเจ้า.";
			next;
			while(1) {
				input .@name$;
				if (.@name$ != strcharinfo(0)) {
					mes "[Marry Happy]";
					mes "หืมม, เจ้าเขียน";
					mes "ชื่อคู่สมรสของเจ้า";
					mes "ไม่ถูกต้อง. บางทีเจ้า";
					mes "ควรจะ copy และ paste มันดูนะ ?";
					mes "ลองใหม่อีกครั้งน่ะ.";
					next;
				}
				else 
					break;
			}
			mes "[Marry Happy]";
			mes "เยี่ยม, ดูเหมือนว่าพวกเรา";
			mes "จะทำพิธีเสร็จสิ้นแล้ว.";
			mes "จำไว้ว่าเจ้าต้อง";
			mes "บอกบาทหลวง Vomars ด้วย";
			mes "ชื่อคู่สมรสของเจ้า เมื่อ";
			mes "เจ้าคุยกับเค้าภายหลัง, okay?";
			next;
			mes "[Marry Happy]";
			mes "เมื่อคู่สมรสของเจ้า";
			mes "ทำพิธีเสร็จเหมือนกัน";
			mes "พวกเจ้าทั้งคู่";
			mes "ต้องคุยกับบาทหลวงเพื่อ";
			mes "งานแต่งงาน.";
			emotion ET_THROB;
			next;
			mes "[Marry Happy]";
			if (Sex == SEX_FEMALE) {
				mes "เจ้าเป็นเจ้าสาว,";
				mes "เจ้าต้องรอให้";
				mes "เจ้าบ่าวคุยกับบาทหลวง";
				mes "Vomars ก่อน. เมื่อเค้า";
				mes "เสร็จแล้ว, ถึงเป็นตาของเจ้า";
				mes "ที่จะคุยกับบาทหลวง Vomars.";
				if (.@covenant)
					delitem 6026,1; //Marriage_Covenant
				else
					set Zeny, Zeny-1200000;
				delitem 2338,1; //Wedding_Dress
			}
			else {
				mes "เจ้าเป็นเจ้าบ่าว,";
				mes "เจ้าต้องคุยกับ";
				mes "บาทหลวงก่อน. เมื่อเจ้า";
				mes "เสร็จแล้ว, มันถึงเป็นตาของ";
				mes "เจ้าสาวที่จะคุยกับ";
				mes "บาทหลวง Vomars.";
				if (.@covenant)
					delitem 6026,1; //Marriage_Covenant
				else
					set Zeny, Zeny-1300000;
				delitem 7170,1; //Tuxedo
			}
			delitem 2613,1; //Diamond_Ring
			set wedding_sign,1;
			goto L_End;
		}
		mes "[Marry Happy]";
		mes "ไม่...?";
		mes "เยี่ยม, เมื่อเจ้า";
		mes "พร้อมที่จะแต่งงาน,";
		mes "ให้กลับมาหาข้า";
		mes "เมื่อเจ้าเปลี่ยนใจ,";
		mes "okay? ขอให้โชคดี~";
		break;
	case 4:
		cutin "wedding_marry02",2;
		donpcevent "Single Army#Prontera::OnEnable";
		donpcevent "Single Army#Geffen::OnEnable";
		donpcevent "Single Army#Morocc::OnEnable";
		donpcevent "Single Army#Payon::OnEnable";
		donpcevent "Single Army#Amatsu::OnEnable";
		donpcevent "Single Army#Gonryun::OnEnable";
		emotion ET_HUK;
		mes "[Single Army]";
		mes "^CC9933เจ้ามี";
		mes "ของสวมใส่ที่";
		mes "สุดยอดไปเลย!^000000";
		emotion ET_ROCK, getnpcid(0, "Single Army#Prontera");
		next;
		mes "[Single Army]";
		mes "^330099ข้าสูญเสีย";
		mes "ปาร์ตี้ไปใน";
		mes "ดันเจี้ยน! แต่ข้า";
		mes "สู้คนเดียวได้!^000000";
		emotion ET_ROCK, getnpcid(0, "Single Army#Geffen");
		next;
		mes "[Single Army]";
		mes "^666666ตอนข้าเป็นเด็กฝึกหัด";
		mes "ข้าได้พยายาม";
		mes "ด้วยตัวข้าเอง";
		mes "เพื่อเปลี่ยนอาชีพ!^000000";
		emotion ET_ROCK, getnpcid(0, "Single Army#Morocc");
		next;
		mes "[Single Army]";
		mes "^666600ข้าเลือกที่จะ";
		mes "อยู่คนเดียวในวัน Christmas...";
		mes "เล่นเกมไพ่และ";
		mes "ปริศนาอักษรไขว้!^000000";
		emotion ET_ROCK, getnpcid(0, "Single Army#Payon");
		next;
		mes "[Single Army]";
		mes "^CC9966ผู้หญิงทำลาย";
		mes "จิตวิญญาณของข้า, แต่ไม่สามารถทำลาย";
		mes "อิสระภาพของข้าได้!^000000";
		emotion ET_ROCK, getnpcid(0, "Single Army#Amatsu");
		next;
		mes "[Single Army]";
		mes "^669900...พวกเราเป็นอิสระ! พวกเรา";
		mes "คือกองทัพคนโสด!^000000";
		emotion ET_ROCK, getnpcid(0, "Single Army#Gonryun");
		close2;
		cutin "wedding_marry01",255;
		emotion ET_SWEAT;
		donpcevent "Single Army#Prontera::OnInit";
		donpcevent "Single Army#Geffen::OnInit";
		donpcevent "Single Army#Morocc::OnInit";
		donpcevent "Single Army#Payon::OnInit";
		donpcevent "Single Army#Amatsu::OnInit";
		donpcevent "Single Army#Gonryun::OnInit";
		end;
	}
	close2;
	cutin "wedding_marry01",255;
	end;

L_End:
	close2;
	cutin "",255;
	end;
}

prt_church,97,102,0	script	Single Army#Prontera	105,{
	mes "[Single Army]";
	mes "^CC9933เจ้ามี";
	mes "ของสวมใส่ที่";
	mes "สุดยอดไปเลย!^000000";
	close;

OnInit:
	hideonnpc "Single Army#Prontera";
	end;

OnEnable:
	hideoffnpc "Single Army#Prontera";
	emotion ET_GO;
	end;
}

prt_church,98,102,0	script	Single Army#Geffen	705,{
	mes "[Single Army]";
	mes "^330099ข้าสูญเสีย";
	mes "ปาร์ตี้ไปใน";
	mes "ดันเจี้ยน! แต่ข้า";
	mes "สู้คนเดียวได้!^000000";
	close;

OnInit:
	hideonnpc "Single Army#Geffen";
	end;

OnEnable:
	hideoffnpc "Single Army#Geffen";
	emotion ET_GO;
	end;
}

prt_church,99,102,0	script	Single Army#Morocc	707,{
	mes "[Single Army]";
	mes "^666666ตอนข้าเป็นเด็กฝึกหัด";
	mes "ข้าได้พยายาม";
	mes "ด้วยตัวข้าเอง";
	mes "เพื่อเปลี่ยนอาชีพ!^000000";
	close;

OnInit:
	hideonnpc "Single Army#Morocc";
	end;

OnEnable:
	hideoffnpc "Single Army#Morocc";
	emotion ET_GO;
	end;
}

prt_church,100,102,0	script	Single Army#Payon	708,{
	mes "[Single Army]";
	mes "^666600ข้าเลือกที่จะ";
	mes "อยู่คนเดียวในวัน Christmas...";
	mes "เล่นเกมไพ่และ";
	mes "ปริศนาอักษรไขว้!^000000";
	close;

OnInit:
	hideonnpc "Single Army#Payon";
	end;

OnEnable:
	hideoffnpc "Single Army#Payon";
	emotion ET_GO;
	end;
}

prt_church,101,102,0	script	Single Army#Amatsu	767,{
	mes "[Single Army]";
	mes "^CC9966ผู้หญิงทำลาย";
	mes "จิตวิญญาณของข้า, แต่ไม่สามารถทำลาย";
	mes "อิสระภาพของข้าได้!^000000";
	close;

OnInit:
	hideonnpc "Single Army#Amatsu";
	end;

OnEnable:
	hideoffnpc "Single Army#Amatsu";
	emotion ET_GO;
	end;
}

prt_church,102,102,0	script	Single Army#Gonryun	780,{
	mes "[Single Army]";
	mes "^669900...พวกเราเป็นอิสระ! พวกเรา";
	mes "คือกองทัพคนโสด!^000000";
	close;

OnInit:
	hideonnpc "Single Army#Gonryun";
	end;

OnEnable:
	hideoffnpc "Single Army#Gonryun";
	emotion ET_GO;
	end;
}

prt_church,100,128,4	script	Bishop#w	60,{
	cutin "wedding_bomars01",2;
	if (Upper == 2) {
		mes "[Vomars]";
		mes "ยินดีที่ได้พบ, เด็กน้อย.";
		mes "เจ้าหลงทางมารึ ? หืมมม.";
		mes "เจ้ารู้ไหมว่า";
		mes "แม่และพ่อเจ้าอยู่ที่ไหน ?";
		close2;
		cutin "wedding_bomars01",255;
		end;
	}

	if (!getpartnerid()) {
		if (!$@wedding) {
			if (wedding_sign == 1) {
				getpartymember(getcharid(1));
				set .@partymembercount,$@partymembercount;
				if (.@partymembercount == 2) {
					if (Sex == SEX_MALE) {
						set $@wedding,1;
						initnpctimer;
						mes "[Vomars]";
						mes "คู่รักหนุ่มสาว, โปรด";
						mes "จำช่วงเวลานี้ไว้";
						mes "ตลอดชีวิตของเจ้า. ขอให้อนาคตของเจ้ามีความสุข";
						mes "และสนุก. ขอให้ความรักของเจ้า";
						mes "เบ่งบานขึ้นทุกๆวัน.";
						next;
						mapannounce "prt_church","เป็นงานแต่งงานจากเจ้าบ่าว, นาย. " + strcharinfo(0) + "...",bc_map;
						mes "[Vomars]";
						mes "จนกว่าโลกจะถึงจุดจบ";
						mes "เจ้าสามารถยืน";
						mes "เคียงข้างกันได้ไหม";
						mes "กับคนที่เจ้ารัก, ให้การช่วยเหลือ";
						mes "เธอ และปกป้องเธอ. ตอนนี้,";
						mes "ข้ารู้ชื่อเจ้าสาวของเจ้าได้ยัง ?";
						next;
						input $@wed_bride$;
						mes "[Vomars]";
						mes "นาย. " + strcharinfo(0) + "...";
						mes "เจ้าสาบานได้ไหมว่า";
						mes "จะดูแลและรักเจ้าสาว";
						mes "ตลอดชีวิตของเจ้า,";
						mes "นางสาว " + $@wed_bride$ + "?";
						next;
						select("สาบาน.");
						set $@wed_groom$,strcharinfo(0);
						mes "[Vomars]";
						mes "ตอนนี้, ถึงเวลาแล้ว";
						mes "ที่เจ้าสาวของเจ้า";
						mes "จะให้คำปฏิญาณตน.";
						mes "ให้ก้าวมาข้างหน้า...";
						close2;
						mapannounce "prt_church","เจ้าบ่าว, นาย. " + strcharinfo(0) + ", ได้ให้คำปฏิญาณตนกับนางสาว " + $@wed_bride$ + "...",bc_map;
						cutin "",255;
						end;
					}
					mes "[Vomars]";
					mes "ข้าขอโทษด้วย, แต่เจ้าบ่าว";
					mes "ต้องคุยกับข้าก่อน";
					mes "มันเป็นประเพณี.";
					mes "มาตั้งแต่สมัยก่อนแล้ว,";
					mes "แต่ข้ายอมรับว่า";
					mes "มันอาจทำให้งานเสร็จเร็วขึ้น.";
					goto L_End;
				}
				mes "[Vomars]";
				mes "ก่อนที่จะ";
				mes "แต่งงาน, เจ้าต้อง";
				mes "สร้างปาร์ตี้";
				mes "สำหรับเจ้าสองคน.";
				mes "ก่อนถึงจะเริ่ม";
				mes "งานแต่งงานได้.";
				goto L_End;
			}
			mes "[Vomars]";
			mes "เจ้าต้องลงทะเบียน";
			mes "งานแต่งกับ Happy Marry";
			mes "ก่อนหน้าที่จะเริ่มงานแต่งงาน.";
			mes "Happy Marry จะบอก";
			mes "ให้เจ้าเตรียมตัว";
			mes "ให้พร้อมก่อนที่จะแต่งงาน.";
			goto L_End;
		}
		else if ($@wedding == 1) {
			if (wedding_sign == 1) {
				getpartymember(getcharid(1));
				set .@partymembercount,$@partymembercount;
				if (.@partymembercount == 2) {
					if (Sex == SEX_FEMALE) {
						if (strcharinfo(0) == $@wed_bride$) {
							mes "[Vomars]";
							mes "คู่รักหนุ่มสาว, โปรด";
							mes "จำช่วงเวลานี้ไว้";
							mes "ตลอดชีวิตของเจ้า. ขอให้อนาคตของเจ้ามีความสุข";
							mes "และสนุก. ขอให้ความรักของเจ้า";
							mes "เบ่งบานขึ้นทุกๆวัน.";
							next;
							mapannounce "prt_church","ลองฟังสิ่งที่เจ้าสาว, นางสาว "+$@wed_bride$+", จะพูด...",bc_map;
							mes "[Vomars]";
							mes "นางสาว "+$@wed_bride$+"...";
							mes "เจ้าสาบานจะอยู่";
							mes "กับ "+$@wed_groom$+",";
							mes "จะอยู่เคียงข้างกัน, ไม่ว่าจะมี";
							mes "อุปสรรคมาขวางกั้นหรือไม่ ?";
							next;
							if (select("^FF0000ไม่.^000000:ค่ะ.") == 1) {
								cutin "wedding_bomars03",2;
								mapannounce "prt_church","คู่ต่อไป, แต่งต่อได้เลย...",bc_map;
								mes "[Vomars]";
								mes "งั้น "+$@wed_groom$;
								mes "ไม่ใช่คู่รัก";
								mes "ที่จะแต่งงานใช่ไหม ? หืมม.";
								mes "ข้าขอโทษด้วยสำหรับเรื่อง";
								mes "เข้าใจผิด...";
								set $@wedding,0;
								close2;
								stopnpctimer;
								cutin "",255;
								end;
							}
							mes "[Vomars]";
							mes "เจ้าจะสาบานด้วย";
							mes "ความซื่อสัตย์หรือไม่ ?";
							mes "ว่าเจ้าจะแต่งกับ "+$@wed_groom$+"?";
							next;
							if (select("สาบานค่ะ.:^FF0000ไม่.^000000") == 1) {
								if (isloggedin(getcharid(3,$@wed_groom$))) {
									if (marriage($@wed_groom$)) {
										//Call Wedding effect
										wedding;
										//Give ring to Bride, and change to wedding sprite.
										sc_start SC_Wedding,3600000,1;
										getitem 2635,1; //Bride_Ring
										//Give ring to Groom, and change to wedding sprite.
										attachrid(getcharid(3,$@wed_groom$));
										sc_start SC_Wedding,3600000,1;
										getitem 2634,1; //Bridegroom_Ring
										detachrid;
										//Switch Script progression back to Bride
										attachrid(getcharid(3,$@wed_bride$));
										cutin "wedding_bomars02",2;
										mapannounce "prt_church","ข้าขอประกาศว่า, "+$@wed_groom$+" และ "+$@wed_bride$+", เป็นสามีและภรรยากันแล้ว.",bc_map;
										mes "[Vomars]";
										mes "ด้วยอำนาจ";
										mes "บาทหลวงแห่ง";
										mes "อาณาจักร Rune-Midgarts,";
										mes "ข้าขอประกาศว่าพวกเจ้าเป็นสามี";
										mes "และภรรยากันแล้ว. ขอให้อนาคต";
										mes "พบเจอแต่ความสุขเถิด.";
										next;
										mes "[Vomars]";
										mes "และสุดท้าย...";
										mes "ขอให้ความสุข,";
										mes "มีแก่ "+$@wed_bride$+"...";
										set $@wed_groom$,"";
										set $@wed_bride$,"";
										set $@wedding,0;
										close2;
										stopnpctimer;
										cutin "",255;
										detachrid;
										end;
									}
								}
								cutin "wedding_bomars03",2;
								mes "[Vomars]";
								mes "หืม. ดูเหมือนว่า";
								mes "เจ้าบ่าวของเจ้าได้หนีจาก";
								mes "พิธีแต่งงานไปแล้ว";
								mes "กรุณานำตัวเค้า";
								mes "กลับมา.";
								goto L_End;
							}
							else {
								cutin "wedding_bomars03",2;
								mapannounce "prt_church","อะร๊า! "+$@wed_bride$+" ปฏิเสธ "+$@wed_groom$+"ที่จะแต่งงาน!",bc_map;
								mes "[Vomars]";
								mes "หืม. ดูเหมือนว่า";
								mes "เจ้าจะเปลี่ยนใจ";
								mes "ข้ารู้สึก";
								mes "เสียใจกับเจ้าบ่าวของเจ้าด้วย,";
								mes "แต่เจ้าต้องเชื่อมั่นในหัวใจของเจ้า";
								mes "ในตอนนี้, วิ่งไปซะ!";
							}
							set $@wed_groom$,"";
							set $@wed_bride$,"";
							set $@wedding,0;
							close2;
							stopnpctimer;
							cutin "",255;
							end;
						}
						callsub S_Busy;
					}
					callsub S_Busy;
				}
				callsub S_Busy;
			}
			if (strcharinfo(0) == $@wed_bride$) {
				mes "[Vomars]";
				mes "หืม? ดูเหมือนว่า";
				mes "Happy Marry ยังคง";
				mes "ไม่รับการลงทะเบียน";
				mes "แต่งงานของเจ้า. กรุณาคุย";
				mes "กับเธออีกครั้ง";
				mes "เพื่องานแต่งงาน.";
				goto L_End;
			}
			callsub S_Busy;
		}
		callsub S_Busy;
	}
	mes "[Vomars]";
	mes "ข้าหวังว่าเจ้าจะมี";
	mes "ความสุขมากๆ. ไม่ว่า";
	mes "จะมีความทุกข์ใดๆ";
	mes "แค่พวกเจ้าฟันผ่า";
	mes "อุปสรรคไปด้วยกันและ";
	mes "ผ่านสิ่งนั้นไปได้.";

L_End:
	close2;
	cutin  "",255;
	end;

S_Busy:
	if ($@wed_groom$ != "" && $@wed_bride$ != "") {
		mes "[Vomars]";
		mes "งานแต่งของ";
		mes "นางสาว "+ $@wed_bride$ +" และ";
		mes "นาย "+ $@wed_groom$;
		mes "กำลังดำเนินการอยู่.";
		mes "กรุณาเบาเสียงลงหน่อย.";
		goto L_End;
	}
	mes "[Vomars]";
	mes "ข้าเป็นผู้ดำเนินการงานแต่ง";
	mes "ของคู่รักคู่อื่นอยู่ตอนนี้, ดังนั้น";
	mes "กรุณารออย่างอดทนจนกว่าจะถึงตาของเจ้า";
	mes "ขอบคุณที่เข้าใจ...";
	goto L_End;

OnStop:
	stopnpctimer;
	end;

OnReset:
	set $@wed_groom$,"";
	set $@wed_bride$,"";
	set $@wedding,0;
	end;

OnTimer180000:
	mapannounce "prt_church","เจ้าตอบช้าเกินไป... คู่ต่อไป, กรุณาเตรียมพร้อม.",bc_map;
	donpcevent "Bishop#w::OnReset";
	stopnpctimer;
	end;
}

prt_church,28,178,4	script	The King of Midgart	108,{
	callfunc "F_GM_NPC";
	mes "[Vomars]";
	mes "เจ้าเป็นใคร ?";
	mes "เจ้ารู้";
	mes "รหัสผ่านของ";
	mes "พลังข้าได้ไง.";
	next;
	if (callfunc("F_GM_NPC",1854,0) < 1) {
		mes "[Vomars]";
		mes "สถานที่นี้";
		mes "ไม่มีที่";
		mes "ที่จะทำตัวงี่เง่าได้.";
		close2;
		warp "prt_church",101,102;
		end;
	}
	mes "[Wedding Switch]";
	mes "ดูเหมือนว่าจะมีปัญหา";
	mes "ในพิธีแต่งงาน ?";
	mes "ข้าสามารถ reset บาทหลวง";
	mes "Vomars NPC ได้ ถ้าเจ้าต้องการ.";
	next;
	switch(select("ไม่, ขอบคุณ:RESET")) {
	case 1:
		mes "[Wedding Switch]";
		mes "เข้าใจล่ะ.";
		mes "อย่างไรก็ตาม, ถ้า";
		mes "บาทหลวง Vomars";
		mes "NPC ค้าง, มันอาจจะ";
		mes "ดีกว่าถ้า reset มัน.";
		close;
	case 2:
		donpcevent "Bishop#w::OnStop";
		donpcevent "Bishop#w::OnReset";
		mapannounce "prt_church","เจ้าตอบช้าเกินไป... คู่ต่อไป, กรุณาเตรียมพร้อม.",bc_map;
		mes "[Wedding Switch]";
		mes "บาทหลวง Vomars NPC";
		mes "ได้เริ่มทำงานใหม่แล้ว.";
		mes "ตอนนี้สามารถ";
		mes "จัดงานแต่งงานได้แล้ว.";
		close;
	}
}

prt_church,20,179,4	script	Divorce Staff	71,{
	callfunc "F_GM_NPC";
	mes "[Bad Ending]";
	mes "อ่า โอ้...";
	mes "เจ้าก็รู้ว่าข้า";
	mes "ทำมันให้เจ้าไม่ได้.";
	next;
	if (callfunc("F_GM_NPC",1854,0) < 1) {
		mes "[Bad Ending]";
		mes "หืม...";
		mes "เจ้า";
		mes "ไม่ควร";
		mes "อยู่ที่นี้...";
		close;
	}
	mes "[Bad Ending]";
	mes "เยี่ยม, เจ้ารู้";
	mes "รหัสผ่าน! แล้วเจ้า";
	mes "ต้องการให้ข้าลบ";
	mes "Wedding Ring ใน";
	mes "กระเป๋าของเจ้า ?";
	next;
	switch(select("ลบ 1 Wedding Ring.:เก็บไว้.")) {
	case 1:
		set .@ring, (Sex == SEX_MALE)?2634:2635; //Bridegroom_Ring, Bride_Ring
		if (countitem(.@ring)) {
			delitem .@ring,1;
			mes "[Bad Ending]";
			mes "เรียบร้อย!";
		} else {
			mes "[Bad Ending]";
			mes "ข้าหา";
			mes "Wedding Ring ไม่เจอ...";
			mes "เจ้าแน่ใจไหม";
			mes "ว่าไม่ได้สวมมันอยู่.";
		}
		close;
	case 2:
		mes "[Bad Ending]";
		mes "เจ้าต้องการ";
		mes "เก็บแหวนไว้ใช่ไหม ?";
		mes "เข้าใจแล้ว, แต่ถ้าเกิด";
		mes "ปัญหาขึ้น, ให้มาหาข้า.";
		close;
	}
}

prt_church,22,179,4	script	Remarry Staff	71,{
	callfunc "F_GM_NPC";
	mes "[Wedding Again]";
	mes "หืม...?";
	mes "อะไรทำให้";
	mes "เจ้ามาที่นี้ ?";
	next;
	if (callfunc("F_GM_NPC",1854,0) < 1) {
		mes "[Wedding Again]";
		mes "อ้าก!";
		mes "นักผจญภัย";
		mes "เจ้าไม่ควร";
		mes "อยู่ที่นี้!";
		close;
	}
	if (getpartnerid()) {
		mes "[Wedding Again]";
		mes "หืม...";
		mes "ข้าสามารถสร้าง";
		mes "Wedding Ring ได้ถ้า";
		mes "ตัวละครได้แต่งงานแล้ว.";
		close;
	}
	mes "[Wedding Again]";
	mes "อ่า, ข้าเห็นว่าเจ้า";
	mes "รู้รหัสผ่าน.";
	mes "แน่นอน, ถ้าเกิดเจ้า";
	mes "ทำ Wedding Ring หาย,";
	mes "ข้าสามารถสร้างให้ใหม่ได้.";
	next;
	switch(select("สร้าง Wedding Ring ใหม่.:ยกเลิก.")) {
	case 1:
		set .@ring, (Sex == SEX_MALE)?2634:2635; //Bridegroom_Ring, Bride_Ring
		if (countitem(.@ring) || isequipped(.@ring)) {
			mes "[Wedding Again]";
			mes "เดี๋ยว, เดี๋ยว...";
			mes "เจ้าสวม";
			mes "Wedding Ring อยู่. ข้าพนันว่า";
			mes "เจ้าคงไม่อยาก";
			mes "ได้มันเพิ่มหรอกน่ะ.";
		} else {
			getitem .@ring,1;
			mes "[Wedding Again]";
			mes "เอาไป~";
			mes "มันเป็นของเจ้า";
			mes "Wedding Ring อันใหม่!";
		}
		close;
	case 2:
		mes "[Wedding Again]";
		mes "แน่นอน. ถ้าเกิดเจ้า";
		mes "ทำ Wedding Ring หาย,";
		mes "ให้มาหาข้าถ้าเจ้า";
		mes "ต้องการวงใหม่, okay?";
		close;
	}
}
